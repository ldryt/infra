{ config, pkgs, ... }:
let
  hidden = import ../../../secrets/obfuscated.nix;
  ocisSubdomain = "files";
  autheliaSubdomain = "iam";
  gloutonPath = "/mnt/glouton";
  minioDataDirName = "minio-buckets";
  minioPort = 44061;
  ocisBucket = "ocis-blobs";
  ocisOidcID = "ocis-web";
in {
  virtualisation.oci-containers.containers = {
    "ocis" = {
      image =
        "docker.io/owncloud/ocis:4.0.6@sha256:f4e4ebd5abfbc1da667fdc0e072061208597a0afe60ee8792be246deb09eb9b2";
      entrypoint = "/bin/sh";
      cmd = [ "-c" "ocis server" ];
      environment = {
        OCIS_URL = "https://${ocisSubdomain}.${hidden.ldryt.host}";
        PROXY_HTTP_ADDR = "127.0.0.1:44082";
        OCIS_LOG_LEVEL = "info";
        OCIS_LOG_COLOR = "true";
        OCIS_LOG_PRETTY = "true";

        OCIS_OIDC_ISSUER = "https://${autheliaSubdomain}.${hidden.ldryt.host}";
        WEB_OIDC_CLIENT_ID = "${ocisOidcID}";
        WEB_OIDC_SCOPE = "openid profile groups email";
        PROXY_TLS = "false";
        PROXY_AUTOPROVISION_ACCOUNTS = "true";
        PROXY_OIDC_REWRITE_WELLKNOWN = "true";
        # PROXY_ROLE_ASSIGNMENT_DRIVER = "oidc";
        # PROXY_ROLE_ASSIGNMENT_OIDC_CLAIM = "groups";
        PROXY_OIDC_ACCESS_TOKEN_VERIFY_METHOD = "none";

        STORAGE_USERS_DRIVER = "s3ng";
        STORAGE_SYSTEM_DRIVER = "ocis";
        STORAGE_USERS_S3NG_ENDPOINT = "http://127.0.0.1:${toString minioPort}";
        STORAGE_USERS_S3NG_REGION = "default";
        STORAGE_USERS_S3NG_BUCKET = "${ocisBucket}";
        STORAGE_USERS_S3NG_ACCESS_KEY = "\${MINIO_ACCESS_KEY:?error message}";
        STORAGE_USERS_S3NG_SECRET_KEY = "\${MINIO_SECRET_KEY:?error message}";
      };
      environmentFiles =
        [ config.sops.secrets."services/ocis/s3/credentials".path ];
      # secretsConfig contains all the secrets needed by ocis (generated by "ocis init")
      volumes = [
        "ocis-data:/var/lib/ocis"
        "${
          config.sops.secrets."services/ocis/secretsConfig".path
        }:/etc/ocis/ocis.yaml:ro"
      ];
      extraOptions = [ "--network=host" ];
    };
    "minio" = {
      image =
        "docker.io/minio/minio:RELEASE.2024-02-17T01-15-57Z@sha256:e02b92b9df448c6c1a07dada58b690105afed2c344aa5195d36388ed33e19e7a";
      entrypoint = "/bin/sh";
      cmd = [ "-c" "mkdir -p /data/${ocisBucket} && minio server /data" ];
      environment = {
        MINIO_ACCESS_KEY = "\${MINIO_ACCESS_KEY:?error message}";
        MINIO_SECRET_KEY = "\${MINIO_SECRET_KEY:?error message}";
      };
      environmentFiles =
        [ config.sops.secrets."services/ocis/s3/credentials".path ];
      volumes = [ "${gloutonPath}/${minioDataDirName}:/data" ];
      ports = [ "${toString minioPort}:9000" ];
    };
  };

  environment.systemPackages = [ pkgs.cifs-utils ];
  fileSystems."${gloutonPath}/${minioDataDirName}" = {
    device = hidden.kiwi.smb.glouton.${minioDataDirName}.shareName;
    fsType = "cifs";
    options = [
      "x-systemd.automount,noauto,x-systemd.idle-timeout=60,x-systemd.device-timeout=5s,x-systemd.mount-timeout=5s,credentials=${
        config.sops.secrets."system/smb/glouton/${minioDataDirName}/credentials".path
      },uid=${toString config.users.users.colon.uid},cache=loose,fsc,sfu"
    ];
  };

  services.nginx = {
    virtualHosts."${ocisSubdomain}.${hidden.ldryt.host}" = {
      enableACME = true;
      forceSSL = true;
      locations."/" = {
        recommendedProxySettings = true;
        proxyPass =
          "http://${config.virtualisation.oci-containers.containers.ocis.environment.PROXY_HTTP_ADDR}";
        extraConfig = ''
          proxy_buffers 4 256k;
          proxy_buffer_size 128k;
          proxy_busy_buffers_size 256k;

          client_max_body_size 0;
        '';
      };
    };
  };

  services.authelia.instances."ldryt".settings.identity_providers.oidc = {
    access_token_lifespan = "2d";
    refresh_token_lifespan = "3d";

    # https://doc.owncloud.com/ocis/next/deployment/services/s-list/idp.html
    clients = [
      {
        description = "ownCloud Web";
        id = "${ocisOidcID}";
        public = true;
        consent_mode = "implicit";
        scopes = [ "email" "groups" "openid" "profile" ];
        redirect_uris = [
          "https://${ocisSubdomain}.${hidden.ldryt.host}/"
          "https://${ocisSubdomain}.${hidden.ldryt.host}/oidc-callback.html"
          "https://${ocisSubdomain}.${hidden.ldryt.host}/oidc-silent-redirect.html"
        ];
      }
      {
        description = "ownCloud Desktop";
        id = "xdXOt13JKxym1B1QcEncf2XDkLAexMBFwiT9j6EfhhHFJhs2KM9jbjTmf8JBXE69";
        secret =
          "UBntmLjC2yYCeHwsyj73Uwo9TAaecAetRwMw0xYcvNL9yRdLSUi0hUAHfvCHFeFh";
        consent_mode = "implicit";
        scopes = [ "email" "groups" "openid" "profile" "offline_access" ];
        redirect_uris = [ "http://127.0.0.1" "http://localhost" ];
        grant_types = [ "refresh_token" "authorization_code" ];
      }
      {
        description = "ownCloud Android";
        id = "e4rAsNUSIUs0lF4nbv9FmCeUkTlV9GdgTLDH1b5uie7syb90SzEVrbN7HIpmWJeD";
        secret =
          "dInFYGV33xKzhbRmpqQltYNdfLdJIfJ9L5ISoKhNoT9qZftpdWSP71VrpGR9pmoD";
        consent_mode = "implicit";
        scopes = [ "email" "groups" "openid" "profile" "offline_access" ];
        redirect_uris = [ "oc://android.owncloud.com" ];
        grant_types = [ "refresh_token" "authorization_code" ];
      }
      {
        description = "ownCloud iOS";
        id = "mxd5OQDk6es5LzOzRvidJNfXLUZS2oN3oUFeXPP8LpPrhx3UroJFduGEYIBOxkY1";
        secret =
          "KFeFWWEZO9TkisIQzR3fo7hfiMXlOpaqP8CFuTbSHzV1TUuGECglPxpiVKJfOXIx";
        consent_mode = "implicit";
        scopes = [ "email" "groups" "openid" "profile" "offline_access" ];
        redirect_uris = [ "oc://ios.owncloud.com" "oc.ios://ios.owncloud.com" ];
        grant_types = [ "refresh_token" "authorization_code" ];
      }
    ];
  };
}
